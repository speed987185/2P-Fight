extends Control

class_name SkinMenu

const BUTTON_CLICK_SOUND: AudioStream = preload("res://assets/Sfx/button click.mp3")

@onready var p1_skin_container: HBoxContainer = $VBoxContainer/Player1Skins
@onready var p2_skin_container: HBoxContainer = $VBoxContainer/Player2Skins
@onready var back_button: Button = $VBoxContainer/BackButton
@onready var play_button: Button = $VBoxContainer/PlayButton

var skin_variants: Array[int] = [0, 1, 2, 3] # 0 for normal, 1, 2, 3 for variants
var skin_containers: Dictionary[int, HBoxContainer] = {}

func _ready() -> void:
	skin_containers[1] = p1_skin_container
	skin_containers[2] = p2_skin_container
	_create_skin_buttons(p1_skin_container, 1)
	_create_skin_buttons(p2_skin_container, 2)

	back_button.pressed.connect(_on_back_pressed)
	play_button.pressed.connect(_on_play_pressed)
	
	if multiplayer.multiplayer_peer and multiplayer.multiplayer_peer.get_connection_status() == MultiplayerPeer.CONNECTION_CONNECTED:
		if not multiplayer.is_server():
			play_button.disabled = true

func _create_skin_buttons(container: HBoxContainer, player_num: int) -> void:
	var gm: GameManager = GameManager.instance
	var current_skin = 0
	if gm != null:
		current_skin = gm.player1_skin_id if player_num == 1 else gm.player2_skin_id

	var white_sb = StyleBoxFlat.new()
	white_sb.bg_color = Color.WHITE
	white_sb.border_width_left = 4
	white_sb.border_width_top = 4
	white_sb.border_width_right = 4
	white_sb.border_width_bottom = 4
	white_sb.border_color = Color.BLACK
	white_sb.corner_radius_top_left = 32
	white_sb.corner_radius_top_right = 32
	white_sb.corner_radius_bottom_left = 32
	white_sb.corner_radius_bottom_right = 32
	white_sb.shadow_color = Color(0, 0, 0, 0.5)
	white_sb.shadow_size = 0
	white_sb.shadow_offset = Vector2(0, 6)
	
	var white_sb_hover = white_sb.duplicate()
	white_sb_hover.bg_color = Color(0.9, 0.9, 0.9)
	white_sb_hover.shadow_offset = Vector2(0, 8)
	
	var white_sb_pressed = white_sb.duplicate()
	white_sb_pressed.bg_color = Color(0.8, 0.8, 0.8)
	white_sb_pressed.shadow_offset = Vector2(0, 2)

	for variant_id in skin_variants:
		var button: Button = Button.new()
		button.custom_minimum_size = Vector2(200, 200)
		button.pivot_offset = Vector2(100, 100)
		button.set_meta("variant_id", variant_id)
		
		button.add_theme_stylebox_override("normal", white_sb)
		button.add_theme_stylebox_override("hover", white_sb_hover)
		button.add_theme_stylebox_override("pressed", white_sb_pressed)
		
		var tex_path = "res://assets/player/improved/s.png"
		if variant_id == 1: tex_path = "res://assets/player/improved/s_b.png"
		elif variant_id == 2: tex_path = "res://assets/player/improved/s_p.png"
		elif variant_id == 3: tex_path = "res://assets/player/improved/s_y.png"
		var tex = load(tex_path)
		var tex_rect = TextureRect.new()
		if tex:
			var logo_tex = AtlasTexture.new()
			logo_tex.atlas = tex
			logo_tex.region = Rect2(0, 0, 214, 239)
			tex_rect.texture = logo_tex
		tex_rect.expand_mode = TextureRect.EXPAND_IGNORE_SIZE
		tex_rect.stretch_mode = TextureRect.STRETCH_KEEP_ASPECT_CENTERED
		tex_rect.set_anchors_preset(PRESET_FULL_RECT)
		tex_rect.mouse_filter = Control.MOUSE_FILTER_IGNORE
		button.add_child(tex_rect)
				
		var tick: Label = Label.new()
		tick.text = "✔"
		tick.name = "TickLabel"
		tick.set_anchors_preset(PRESET_FULL_RECT)
		tick.horizontal_alignment = HORIZONTAL_ALIGNMENT_RIGHT
		tick.vertical_alignment = VERTICAL_ALIGNMENT_BOTTOM
		tick.add_theme_font_size_override("font_size", 40)
		tick.add_theme_color_override("font_color", Color(0.2, 0.8, 0.2))
		tick.add_theme_color_override("font_outline_color", Color.BLACK)
		tick.add_theme_constant_override("outline_size", 4)
		tick.visible = (variant_id == current_skin)
		button.add_child(tick)
				
		button.pressed.connect(_on_skin_selected.bind(player_num, variant_id))
		button.mouse_entered.connect(func():
			var tween: Tween = create_tween()
			tween.tween_property(button, "scale", Vector2(1.15, 1.15), 0.15).set_trans(Tween.TRANS_SINE).set_ease(Tween.EASE_OUT)
		)
		button.mouse_exited.connect(func():
			var tween: Tween = create_tween()
			tween.tween_property(button, "scale", Vector2(1.0, 1.0), 0.15).set_trans(Tween.TRANS_SINE).set_ease(Tween.EASE_OUT)
		)
		container.add_child(button)

func _play_sfx(sound: AudioStream) -> void:
	if sound == null:
		return
	var sfx_player: AudioStreamPlayer = AudioStreamPlayer.new()
	sfx_player.stream = sound
	sfx_player.bus = "SFX"
	add_child(sfx_player)
	sfx_player.play()
	sfx_player.finished.connect(sfx_player.queue_free)

func _play_button_click_sound() -> void:
	_play_sfx(BUTTON_CLICK_SOUND)

func _on_skin_selected(player_num: int, variant_id: int) -> void:
	if multiplayer.multiplayer_peer and multiplayer.multiplayer_peer.get_connection_status() == MultiplayerPeer.CONNECTION_CONNECTED:
		if multiplayer.is_server() and player_num == 1:
			rpc("sync_skin", player_num, variant_id)
		elif not multiplayer.is_server() and player_num == 2:
			rpc_id(1, "sync_skin", player_num, variant_id)
	else:
		_apply_skin_selected(player_num, variant_id)

@rpc("any_peer", "call_local", "reliable")
func sync_skin(player_num: int, variant_id: int) -> void:
	if multiplayer.is_server():
		# Server broadcasts to all
		rpc("broadcast_skin", player_num, variant_id)
	_apply_skin_selected(player_num, variant_id)

@rpc("authority", "call_local", "reliable")
func broadcast_skin(player_num: int, variant_id: int) -> void:
	_apply_skin_selected(player_num, variant_id)

func _apply_skin_selected(player_num: int, variant_id: int) -> void:
	_play_button_click_sound()
	var gm: GameManager = GameManager.instance
	if gm != null:
		gm.set_player_skin(player_num, Color.WHITE, variant_id)
	print("Player ", player_num, " selected skin ", variant_id)
	_update_skin_ticks(player_num, variant_id)

func _update_skin_ticks(player_num: int, variant_id: int) -> void:
	var container: HBoxContainer = skin_containers.get(player_num, null)
	if container == null:
		return
	for btn in container.get_children():
		if btn is Button:
			var tick: Label = btn.get_node_or_null("TickLabel")
			if tick:
				tick.visible = (btn.get_meta("variant_id") == variant_id)

func _on_play_pressed() -> void:
	_play_button_click_sound()
	if multiplayer.multiplayer_peer and multiplayer.multiplayer_peer.get_connection_status() == MultiplayerPeer.CONNECTION_CONNECTED:
		if multiplayer.is_server():
			rpc("sync_play")
	else:
		SceneTransition.change_scene("res://Scenes/arena_menu.tscn")

@rpc("authority", "call_local", "reliable")
func sync_play() -> void:
	SceneTransition.change_scene("res://Scenes/arena_menu.tscn")

func _on_back_pressed() -> void:
	_play_button_click_sound()
	if multiplayer.multiplayer_peer:
		multiplayer.multiplayer_peer.close()
		multiplayer.multiplayer_peer = null
	SceneTransition.change_scene("res://Scenes/main_menu.tscn")
